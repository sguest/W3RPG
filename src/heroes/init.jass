import "/heroes/paladin/init.jass"
import "/heroes/farSeer/init.jass"
import "/heroes/demonHunter/init.jass"
import "/heroes/levelStats.jass"
import "/heroes/skillLevels.jass"

function HeroLevelActions takes nothing returns nothing
    local unit hero = GetLevelingUnit()
    call ApplyLevelStats(hero)
    call SetPlayerTechResearched(GetOwningPlayer(hero), 'R000', GetHeroLevel(hero))
    set hero = null
endfunction

function CreateHeroLevelTrigger takes unit hero returns nothing
    local trigger levelTrigger = CreateTrigger()
    call TriggerRegisterUnitEvent(levelTrigger, hero, EVENT_UNIT_HERO_LEVEL)
    call TriggerAddAction(levelTrigger, function HeroLevelActions)
    set levelTrigger = null
endfunction

function InitHero takes unit hero returns nothing
    local integer unitType = GetUnitTypeId(hero)
    call CreateHeroLevelTrigger(hero)

    if (unitType == 'Hpal') then
        call InitPaladin(hero)
    elseif(unitType == 'Ofar') then
        call InitFarSeer(hero)
    elseif(unitType == 'Edem') then
        call InitDemonHunter(hero)
    endif
endfunction

function HeroSoldActions takes nothing returns nothing
    local unit buyer = GetBuyingUnit()
    local unit hero = GetSoldUnit()
    local location targetLocation = GetRectCenter(gg_rct_HeroSpawn)

    call RemoveUnit(buyer)
    call SetUnitPositionLoc(hero, targetLocation)
    call PanCameraToTimedLocForPlayer(GetOwningPlayer(hero), targetLocation, 0)

    set udg_PlayerHeroes[GetPlayerId(GetOwningPlayer(hero))] = hero
    call InitHero(hero)

    call RemoveLocation(targetLocation)
    set targetLocation = null
    set buyer = null
    set hero = null
endfunction

function InitHeroSelectionPlayer takes nothing returns nothing
    local player picked = GetEnumPlayer()
    local location spawnLocation = GetRectCenter(gg_rct_HeroSelection)

    call CreateUnitAtLoc(picked, 'h001', spawnLocation, 270)
    call RemoveLocation(spawnLocation)
    set spawnLocation = null
    set picked = null
endfunction

function InitHeroSelection takes nothing returns nothing
    local trigger heroSoldTrigger = CreateTrigger()
    call TriggerRegisterUnitEvent(heroSoldTrigger, gg_unit_halt_0213, EVENT_UNIT_SELL)
    call TriggerRegisterUnitEvent(heroSoldTrigger, gg_unit_oalt_0001, EVENT_UNIT_SELL)
    call TriggerRegisterUnitEvent(heroSoldTrigger, gg_unit_uaod_0216, EVENT_UNIT_SELL)
    call TriggerRegisterUnitEvent(heroSoldTrigger, gg_unit_eate_0217, EVENT_UNIT_SELL)
    call TriggerAddAction(heroSoldTrigger, function HeroSoldActions)

    call ForForce(GetPlayersAll(), function InitHeroSelectionPlayer)
endfunction

function InitHeroes takes nothing returns nothing
    call InitHeroSelection()
    call InitSkillLevels()
endfunction