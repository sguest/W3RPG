function EarthquakeConditions takes nothing returns boolean
    return (GetSpellAbilityId() == 'A02E')
endfunction

function EarthquakeStartActions takes nothing returns nothing
    local unit caster = GetSpellAbilityUnit()
    local integer handleId = GetHandleId(caster)
    local integer earthquakeHash = StringHash("Earthquake")
    local integer currentIndex
    local integer loadIndex
    local group targetGroup
    local location targetLocation = GetSpellTargetLoc()
    local unit picked
    local real damageAmount

    set currentIndex = LoadInteger(udg_ChannelCasters, earthquakeHash, handleId) + 1
    call SaveInteger(udg_ChannelCasters, earthquakeHash, handleId, currentIndex)

    loop
        set loadIndex = LoadInteger(udg_ChannelCasters, earthquakeHash, handleId)
        exitwhen loadIndex != currentIndex

        set damageAmount = GetHeroInt(caster, true) * 2.0
        set targetGroup = GetUnitsInRangeOfLocAll(250.00, targetLocation)

        loop
            set picked = FirstOfGroup(targetGroup)
            exitwhen picked == null
            call GroupRemoveUnit(targetGroup, picked)

            if(IsPlayerEnemy(GetOwningPlayer(picked), GetOwningPlayer(caster)) and not IsUnitDeadBJ(picked)) then
                call UnitDamageTargetBJ(caster, picked, damageAmount, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_NORMAL)
            endif
        endloop

        call DestroyGroup(targetGroup)
        set targetGroup = null

        call PolledWait(1)
    endloop

    call RemoveLocation(targetLocation)
    set targetLocation = null
endfunction

function EarthquakeEndActions takes nothing returns nothing
    local unit caster = GetSpellAbilityUnit()
    local integer handleId = GetHandleId(caster)
    local integer earthquakeHash = StringHash("Earthquake")
    local integer currentIndex = 0

    if(HaveSavedInteger(udg_ChannelCasters, earthquakeHash, handleId)) then
        set currentIndex = LoadInteger(udg_ChannelCasters, earthquakeHash, handleId)
    endif

    call SaveInteger(udg_ChannelCasters, earthquakeHash, handleId, currentIndex + 1)
endfunction

function InitEarthquake takes unit hero returns nothing
    local trigger earthquakeStartTrigger = CreateTrigger()
    local trigger earthquakeEndTrigger = CreateTrigger()
    call TriggerRegisterUnitEvent(earthquakeStartTrigger, hero, EVENT_UNIT_SPELL_CHANNEL)
    call TriggerRegisterUnitEvent(earthquakeEndTrigger, hero, EVENT_UNIT_SPELL_ENDCAST)
    call TriggerAddCondition(earthquakeStartTrigger, Condition(function EarthquakeConditions))
    call TriggerAddCondition(earthquakeEndTrigger, Condition(function EarthquakeConditions))
    call TriggerAddAction(earthquakeStartTrigger, function EarthquakeStartActions)
    call TriggerAddAction(earthquakeEndTrigger, function EarthquakeEndActions)
endfunction

